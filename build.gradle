plugins {
    id 'com.verificationgentleman.gradle.hdvl.systemverilog' version '0.2.7'
    id 'com.verificationgentleman.gradle.hdvl.c' version '0.2.7'
}

group 'com.verificationgentleman'

sourceSets {
    main {
        sv {
            srcDirs = ['sv']
        }
        c {
            srcDirs = ['c']
        }
    }
}

extensions.xrunDir = file("$buildDir/xrun")

task test(type: Exec) {
    dependsOn genFullXrunArgsFile
    executable 'xrun'
    args'-f', genFullXrunArgsFile.destination.get().asFile
    args file('test/test.sv')
    args '-access', 'rwc'
    workingDir xrunDir
    mkdir workingDir
}

sourceSets {
    paramsExample {
        sv {
            srcDir 'examples/parameters'
        }
    }
}

dependencies {
    paramsExampleCompile files(genXrunArgsFile.destination)
}

abstract class RunExample extends DefaultTask {
    @InputFile
    abstract RegularFileProperty getFullXrunArgsFile()

    @OutputDirectory
    abstract DirectoryProperty getWorkingDir()

    @TaskAction
    def run() {
        def workDir = workingDir
        project.exec {
            executable 'xrun'
            args'-f', fullXrunArgsFile.get().asFile
            args '-access', 'rwc'
            args '-top', 'top'
            workingDir workDir
        }
    }
}

task runParamsExample(type: RunExample) {
    fullXrunArgsFile = genFullParamsExampleXrunArgsFile.destination
    workingDir = file("$buildDir/params-example")
    outputs.upToDateWhen { false }
}
